
plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'maven-publish'
}

group = "com.github.goatfryed"
version = "1.0.0-alpha1-SNAPSHOT"

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()

    registerFeature('json') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('xml') {
        usingSourceSet(sourceSets.main)
    }
}

dependencies {
    implementation 'commons-io:commons-io:2.16.1'
    implementation 'org.apache.commons:commons-lang3:3.16.0'
    implementation 'org.junit.jupiter:junit-jupiter:5.11.0'
    api 'org.assertj:assertj-core:3.26.3'

    jsonApi 'net.javacrumbs.json-unit:json-unit-assertj:3.4.1'
    xmlApi 'org.xmlunit:xmlunit-assertj:2.10.0'

    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
}

testing {
    //noinspection GroovyAssignabilityCheck // experimental gradle api
    suites {

        test {
            useJUnitJupiter()
        }

        testSpi(JvmTestSuite) {
            dependencies {
                implementation project()
            }

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

tasks.named("check") {
    dependsOn(testing.suites.testSpi)
}

tasks.named('javadoc', Javadoc) {
    setFailOnError(true)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'AssertBaseline'
                description = 'AssertJ assertions for baseline testing'
                url = 'https://github.com/goatfryed/assert-baseline'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'goatfryed'
                        name = 'Omar Sood'
                        email = 'goatfryed@gmail.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = 'MavenCentral'
            url = version.endsWith('SNAPSHOT') ?
                    uri('https://s01.oss.sonatype.org/content/repositories/snapshots/') :
                    uri('https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/')
        }
    }
}